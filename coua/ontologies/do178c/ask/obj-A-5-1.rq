# Source Code complies with low-level requirements

PREFIX coua: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/schema#>
PREFIX do178c: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/ontologies/do178c#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

ASK { FILTER NOT EXISTS {
    ?rclass rdfs:subClassOf+ do178c:LowLevelRequirement .
    ?r a ?rclass .

    # Remove all requirements which are implemented in the source code (according to the tracing)
    MINUS {
        ?rp2 rdfs:subPropertyOf+ do178c:requirementId .
        ?r ?rp2 ?rid .

        # Find candidate traces
        ?tc rdfs:subClassOf+ do178c:TraceData .
        ?rp1 rdfs:subPropertyOf+ do178c:requirementId .
        ?e a ?tc ;
            ?rp1 ?rid .
    }
    MINUS{ ?r coua:notImplemented "true"^^xsd:boolean }
}}
