# Source Code complies with low-level requirements

PREFIX do178c: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/ontologies/do178c#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

ASK { FILTER NOT EXISTS {
    do178c:Obj_A_5_1 do178c:hasApplicability ?appl .
    ?appl do178c:toSoftwareLevel ?swl .
    ?sw a do178c:Software ;
        do178c:hasSoftwareLevel ?swl .

    ?rclass rdfs:subClassOf+ do178c:LowLevelRequirement .
    ?r a ?rclass .

    # Remove all requirements which are implemented in the source code (according to the tracing)
    MINUS {
        ?rp2 rdfs:subPropertyOf+ do178c:requirementId .
        ?r ?rp2 ?rid .

        # Find candidate traces
        ?tc rdfs:subClassOf+ do178c:TraceData .
        ?rp1 rdfs:subPropertyOf+ do178c:requirementId .
        ?e a ?tc ;
            ?rp1 ?rid2 .

        # Match up by requirement id
        FILTER(?rid = ?rid2)
    }
}}
