# Low-level requirements are traceable to high-level requirements.

PREFIX do178c: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/ontologies/do178c#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

ASK {
	FILTER NOT EXISTS {
		SELECT ?ReqLow ?ReqHigh
		WHERE
		{
			# Check if this objective is applicable
			do178c:Obj_A_4_6 do178c:hasApplicability ?appl .
			?appl do178c:toSoftwareLevel ?swl .
			?sw a do178c:Software ;
			    do178c:hasSoftwareLevel ?swl .

			?reqLowClass rdfs:subClassOf+ do178c:LowLevelRequirement .
			?ReqLow a ?reqLowClass .
			FILTER NOT EXISTS {
				?reqHighClass rdfs:subClassOf+ do178c:HighLevelRequirement .
				?ReqHigh a ?reqHighClass .
				?tracesProp rdfs:subPropertyOf+ do178c:traces .
				?ReqLow ?tracesProp ?ReqHigh .
			}
		}
	}
}
