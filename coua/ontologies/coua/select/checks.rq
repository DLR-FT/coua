 PREFIX coua: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/schema#>
PREFIX do178c: <https://gitlab.dlr.de/ft-ssy-avs/ap/coua/ontologies/do178c#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?Check ?Status ?Description ?disabled
WHERE
{
  {
    ?checkC rdfs:subClassOf+ coua:Check .
    ?check a ?checkC .
  } UNION {
    ?check a coua:Check
  } .

  ?check coua:status ?status .

  OPTIONAL {
    ?check coua:checkDisabled ?disabled
  }
  OPTIONAL {
    # Check applicability of objective
    ?check do178c:hasApplicability ?appl .
    ?appl do178c:toSoftwareLevel ?swl .
    ?sw a do178c:Software ;
      do178c:hasSoftwareLevel ?swl .
  }
  BIND(
    IF(bound(?disabled),
      "disabled",
      IF(bound(?swl) && !bound(?appl),
        "not applicable",
        IF(?status = "true"^^xsd:boolean,
          "passed",
          "failed"))) AS ?Status)

  ?check rdfs:label ?Check .
  ?check rdfs:comment ?Description .
}
ORDER BY ?Check
